buildscript
{
	repositories
	{
		mavenCentral()
		maven {url 'https://masa.dy.fi/maven/'}
		maven {url 'https://files.minecraftforge.net/maven'}
		maven {url 'https://plugins.gradle.org/m2/'}
		maven {url "https://repo.spongepowered.org/maven"}
	}
	dependencies
	{
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-7764e3e'
		classpath "org.spongepowered:mixingradle:0.6-SNAPSHOT"
		classpath "com.github.jengelman.gradle.plugins:shadow:4.0.4"
	}
}

apply plugin: "java"
apply plugin: "net.minecraftforge.gradle.tweaker-server"
apply plugin: "org.spongepowered.mixin"
apply plugin: "com.github.johnrengelman.shadow"

group = name
version = 'dev'
archivesBaseName = name

minecraft {
	version = "1.12.2"
	mappings = "stable_39"
	runDir = "run"
	tweakClass = "carpet.launch.ServerTweaker"
	serverJvmArgs.addAll("-Dmixin.debug=true")
	makeObfSourceJar = false
}

reobf {
	jar {
		mappingType = "NOTCH"
	}
	shadowJar {
		mappingType = "NOTCH"
	}
}

mixin {
	defaultObfuscationEnv notch
}

sourceSets.main.ext.refMap = "${name}-refmap.json"

repositories {
	maven { url = "https://repo.spongepowered.org/maven" }
	maven { url = "https://libraries.minecraft.net/" }
	maven { url = "https://maven.sk89q.com/repo" }
}

configurations {
	shadowed
	implementation.extendsFrom(shadowed)
}


dependencies {
	shadowed "org.spongepowered:mixin:0.8.1-SNAPSHOT"
	shadowed "org.ow2.asm:asm-debug-all:5.2"
	provided "org.ow2.asm:asm-all:5.2"
	shadowed("net.minecraft:launchwrapper:1.12") {
		exclude group: "org.lwjgl.lwjgl"
	}
	shadowed "org.apache.logging.log4j:log4j-api:2.8.1"
	shadowed "org.apache.logging.log4j:log4j-core:2.8.1"
	implementation "com.eclipsesource.minimal-json:minimal-json:0.9.5"
	implementation "com.google.guava:guava:21.0"
	implementation "org.apache.commons:commons-lang3:3.5"
	implementation "commons-io:commons-io:2.5"
	implementation "com.google.code.gson:gson:2.8.0"

	// World Edit
	implementation "com.sk89q.worldedit:worldedit-core:6.1"
	implementation "com.sk89q:jchronic:0.2.4a"
	implementation "com.thoughtworks.paranamer:paranamer:2.6"
	implementation "com.sk89q.lib:jlibnoise:1.0.0"
}

jar {
	manifest {
		attributes(
			'Main-Class': 'carpet.Main',
			'Class-Path': 'minecraft_server.1.12.2.jar'
		)
	}
}

shadowJar {
	classifier = ""
	configurations = [project.configurations.shadowed]
	exclude "META-INF/maven/**"
	exclude "META-INF/DEPENDENCIES"
	exclude "META-INF/fml_cache_*.json"
	exclude "META-INF/LICENSE"
	exclude "META-INF/log4j-provider.properties"
	exclude "META-INF/NOTICE"
	exclude "META-INF/web-fragment.xml"
	exclude "LICENSE.txt"
}
tasks.build.dependsOn(shadowJar)
