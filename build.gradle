import java.text.SimpleDateFormat

plugins {
	id 'fabric-loom' version '0.4-SNAPSHOT'
}

group = name
archivesBaseName = name

def buildDate = new Date()
if (project.mod_version.endsWith('-dev')) {
	def df = new SimpleDateFormat(".yyyyMMdd.HHmmss")
	df.setTimeZone(TimeZone.getTimeZone("UTC"))
	project.mod_version += df.format(buildDate)
}
version = project.mod_version

repositories {
	maven {
		name = 'Fabric'
		url = 'http://maven.fabricmc.net/'
	}
	maven {
		name = 'SpongePowered'
		url = 'http://repo.spongepowered.org/maven/'
	}
	maven {
		name = 'Mojang'
		url = 'https://libraries.minecraft.net/'
	}
	maven {
		name = 'legacy-fabric'
		url = 'https://dl.bintray.com/legacy-fabric/Legacy-Fabric-Maven'
	}
	maven {
		name = 'skyrising'
		url = 'https://maven.skyrising.xyz/'
	}
	maven {
		name = 'sk89q'
		url = 'https://maven.sk89q.com/repo/'
	}
	maven {
		name = 'Jitpack'
		url = 'https://jitpack.io/'
	}
}

minecraft {
	intermediaryUrl = { version ->
		return "https://dl.bintray.com/legacy-fabric/Legacy-Fabric-Maven/net/fabricmc/intermediary/${version}/intermediary-${version}-v2.jar";
	}
}

dependencies {
	implementation "com.google.guava:guava:23.5-jre"
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	//mappings "de.oceanlabs:mcp:${project.mcp_mappings}:v2"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	// Fix for https://github.com/FabricMC/fabric-loader/issues/152
	modImplementation "com.github.shedaniel:fabric-loader:e4a8c2f8b6"

	// World Edit
	implementation "com.sk89q.worldedit:worldedit-core:6.1"
	implementation "com.sk89q:jchronic:0.2.4a"
	implementation "com.thoughtworks.paranamer:paranamer:2.6"
	implementation "com.sk89q.lib:jlibnoise:1.0.0"
}

processResources {
	def properties = [
			version: project.version
	]
	inputs.properties properties

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand properties
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}